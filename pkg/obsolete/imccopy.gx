# Copyright(c) 1986 Association of Universities for Research in Astronomy Inc.

include	<imhdr.h>

$for (silrdx)
# IMC_COPY -- Copy the input image to the output image.
# This procedure is called when there is only one image to combine.

procedure imc_copy$t (in, out)

pointer	in			# Input IMIO pointer
pointer	out			# Output IMIO pointer

int	nc
pointer	sp, v1, v2, indata, outdata, impnl$t(), imgnl$t()

begin
	call smark (sp)
	call salloc (v1, IM_MAXDIM, TY_LONG)
	call salloc (v2, IM_MAXDIM, TY_LONG)
	call amovkl (long(1), Meml[v1], IM_MAXDIM)
	call amovkl (long(1), Meml[v2], IM_MAXDIM)

	nc = IM_LEN(in,1)
	while ((impnl$t (out, outdata, Meml[v1]) != EOF) &&
	    (imgnl$t (in, indata, Meml[v2]) != EOF))
	    call amov$t (Mem$t[indata], Mem$t[outdata], nc)

	call sfree (sp)
end
$endfor
