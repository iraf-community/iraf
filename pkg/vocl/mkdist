#!/bin/csh -f
#
#  Build the VOCL self-installer distribution
#
###############################################################################


if ((! $?iraf) || (! $?IRAFARCH)) then
    echo -n "ERROR: "
    echo    "Must define 'iraf' and 'IRAFARCH' before building!!"
    echo    "Quitting."
    exit 1
endif


# Process command line arguments.
set	exec	= yes
set src_only	= no

while ("$1" != "")
    switch ("$1")
    case -n:                            # no execute
        set exec = no
        breaksw
    case -src:                          # source-only distribution
        set src_only = yes
        breaksw
    default:
	echo "mkdist: unknown argument $1"
	breaksw
    endsw

    if ("$2" == "") then
        break
    else
        shift
    endif
end


# Do it.
echo "Removing old binaries ...."
  if ($exec == "yes" && $src_only == "no") then
    rmbin .
  endif 
	
echo "Rebuilding VOCL ...."
  if ($exec == "yes" && $src_only == "no") then
    mkpkg relink
  endif 

	
echo "Creating VOCL distribution...."
  if ($exec == "yes") then

    # Exclude other distribution files and platform-specific libs 
    # tarball.
    if ($src_only == "yes") then
        /bin/ls -1 vocl_install_* *.[aeo] */*.[aeo]    > /tmp/_ex$$
    else
        /bin/ls -1 vocl_install_* lib*.a */*.[aeo]     > /tmp/_ex$$
    endif

    # Create a tar of the source and binary
    if ($IRAFARCH == "ssun" || $IRAFARCH == "sparc") then
        tar -cfX - /tmp/_ex$$ . | gzip > /tmp/_tar$$
    else
        tar -cf - -X /tmp/_ex$$ . | gzip > /tmp/_tar$$
    endif

    # Encode the tarball and build the installer itself.
    uuencode /tmp/_tar$$ vocl_tar.gz > /tmp/_uu$$
    if ($src_only == "yes") then
        cat vocl_install.csh /tmp/_uu$$ | sed -e 's/VERSION_DATE/`date`/' \
	    > vocl_install_src.csh
        chmod 777 vocl_install_src.csh
    else
        cat vocl_install.csh /tmp/_uu$$ | sed -e 's/VERSION_DATE/`date`/' \
	    > vocl_install_${IRAFARCH}.csh
        chmod 777 vocl_install_${IRAFARCH}.csh
    endif

    # Clean up.
    /bin/rm -f /tmp/_tar$$ /tmp/_uu$$ /tmp/_ex$$
  endif 
	
echo "Done."
