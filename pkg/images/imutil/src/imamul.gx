# Copyright(c) 1986 Association of Universities for Research in Astronomy Inc.

include	<imhdr.h>

# IMA_MUL -- Image arithmetic multiplication.

$for (silrd)
procedure ima_mul$t (im_a, im_b, im_c, a, b)

pointer	im_a, im_b, im_c
PIXEL	a, b

int	len
pointer	im[3], buf[3]
long	v[IM_MAXDIM, 3]

int	ima_nl$t()

begin
	# Loop through all of the image lines.
	im[1] = im_c
	len = IM_LEN (im[1], 1)
	call amovkl (long(1), v, 3 * IM_MAXDIM)

	# If imagea is constant then read imageb.  If the constant
	# is 1 do a vector move to imagec otherwise do a vector
	# multiply to imagec.
	if (im_a == NULL) {
	    im[2] = im_b
	    while (ima_nl$t (im, buf, v, 2) != EOF) {
		if (a == 1$f)
		    call amov$t (Mem$t[buf[2]], Mem$t[buf[1]], len)
		else
		    call amulk$t (Mem$t[buf[2]], a, Mem$t[buf[1]], len)
	    }

	# If imageb is constant then read imagea.  If the constant
	# is 1 do a vector move to imagec otherwise do a vector
	# multiply to imagec.
	} else if (im_b == NULL) {
	    im[2] = im_a
	    while (ima_nl$t (im, buf, v, 2) != EOF) {
		if (b == 1$f)
		    call amov$t (Mem$t[buf[2]], Mem$t[buf[1]], len)
		else
		    call amulk$t (Mem$t[buf[2]], b, Mem$t[buf[1]], len)
	    }

	# Read imagea and imageb and do the vector multiply to imagec.
	} else {
	    im[2] = im_a
	    im[3] = im_b
	    while (ima_nl$t (im, buf, v, 3) != EOF)
		call amul$t (Mem$t[buf[2]], Mem$t[buf[3]], Mem$t[buf[1]], len)
	}
end
$endfor
