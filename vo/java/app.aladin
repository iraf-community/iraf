#!/bin/csh -f

unalias ps egrep java

set dir   = `dirname $0`
set me    = `whoami`
set pid   = `ps -fwu $me | egrep -v grep | egrep -i Aladin.jar`  #  MACHDEP
set npid  = $#pid
if ($npid == 0) then
    #  No Aladin running, check for a JSAMP/TOPCAT Hub.
    set pid   = `ps -fwu $me | egrep -i hub | egrep -i "jsamp|topcat"`
    set npid  = $#pid
    set is_aladin = 0
else
    set is_aladin = 1
endif

set jar   = Aladin.jar


if ($#argv > 0) then
    if ("$1" == "-bg") then
        if ($npid == 0 && $is_aladin == 0) then
            # No Hub running that we know about, clear out dreg .samp file.
            /bin/rm -rf ~/.samp
        endif
        exec ${dir}/aladin &

    else if ("$1" == "-kill") then
        kill -9 $pid[2]
        /bin/rm -rf ~/.samp

    else if ("$1" == "-status") then
        if ($npid == 0 && $is_aladin == 0) then
	    echo "off"
        else
	    echo "on"
        endif

    endif
else
    if ($npid == 0 && $is_aladin == 0) then
        /bin/rm -rf ~/.samp
    endif
    exec ${dir}/aladin
endif
